#include <cmath>
#include <cstddef>

#include <cub/block/block_reduce.cuh>

#include "glog/logging.h"

#include "infini_train/include/common/maca/common_maca.h"
#include "infini_train/include/common/maca/kernel_helper.cuh"
#include "infini_train/include/dispatcher.h"
#include "infini_train/include/tensor.h"

namespace infini_train::kernels::maca {
template <size_t BLOCK_SIZE, typename T>
__global__ void SoftmaxForwardKernel(T *output, const T *input, int64_t outer_size, int64_t axis_size,
                                     int64_t inner_size) {
    using BlockReduce = cub::BlockReduce<float, BLOCK_SIZE>;

    __shared__ typename BlockReduce::TempStorage temp_storage_max;
    __shared__ typename BlockReduce::TempStorage temp_storage_sum;
    __shared__ float row_max;
    __shared__ float row_sum;

    const int64_t group = blockIdx.x;     // row of the grid
    const int64_t inner_idx = blockIdx.y; // column of the grid
    const int tid = threadIdx.x;

    // calculate the maximum for each group
    float thread_max = -INFINITY;
    for (int64_t axis = tid; axis < axis_size; axis += BLOCK_SIZE) {
        int64_t idx = (group * axis_size + axis) * inner_size + inner_idx;
        thread_max = max(thread_max, common::maca::Cast<float>(input[idx]));
    }
    float block_max = BlockReduce(temp_storage_max).Reduce(thread_max, cub::Max());

    if (tid == 0) {
        row_max = block_max;
    }
    __syncthreads();

    // calculate the sum of exponents
    float thread_sum = 0;
    for (int64_t axis = tid; axis < axis_size; axis += BLOCK_SIZE) {
        int64_t idx = (group * axis_size + axis) * inner_size + inner_idx;
        float exp_val = exp(common::maca::Cast<float>(input[idx]) - row_max);
        output[idx] = common::maca::Cast<T>(exp_val);
        thread_sum += exp_val;
    }
    float block_sum = BlockReduce(temp_storage_sum).Sum(thread_sum);

    if (tid == 0) {
        row_sum = block_sum;
    }
    __syncthreads();

    // normalize
    for (int64_t axis = tid; axis < axis_size; axis += BLOCK_SIZE) {
        int64_t idx = (group * axis_size + axis) * inner_size + inner_idx;
        output[idx] = common::maca::Cast<T>(common::maca::Cast<float>(output[idx]) / row_sum);
    }
}

template <size_t BLOCK_SIZE, typename T>
void LaunchForward(const std::shared_ptr<Tensor> &output, const std::shared_ptr<Tensor> &input, int64_t dim) {
    const auto &input_dims = input->Dims();
    int64_t outer_size = 1;
    int64_t axis_size = input_dims[dim];
    int64_t inner_size = 1;

    for (int i = 0; i < dim; ++i) { outer_size *= input_dims[i]; };
    for (int i = dim + 1; i < input_dims.size(); ++i) { inner_size *= input_dims[i]; };
    if (axis_size == 0) {
        LOG_LOC(INFO, "MACA softmax forward: 'input_dims[dim] == 0'");
        return;
    }
    if (outer_size == 0) {
        return;
    }

    T *output_ptr = static_cast<T *>(output->DataPtr());
    const T *input_ptr = static_cast<const T *>(input->DataPtr());

    if (BLOCK_SIZE > 1024) {
        LOG_LOC(FATAL, "MACA softmax forward: 'BLOCK_SIZE used is larger than the max number of thread per block'");
    }
    dim3 block_dims(BLOCK_SIZE);
    dim3 grid_dims(outer_size, inner_size);

    const auto *maca_device = dynamic_cast<const MacaDevice *>(output->GetDevice());
    maca_device->SetDevice();
    SoftmaxForwardKernel<BLOCK_SIZE, T>
        <<<grid_dims, block_dims, 0, maca_device->Stream()>>>(output_ptr, input_ptr, outer_size, axis_size, inner_size);
}

std::shared_ptr<Tensor> SoftmaxForward(const std::shared_ptr<Tensor> &input, int64_t dim) {
    auto dtype = input->Dtype();
    const auto &input_dims = input->Dims();
    dim = dim < 0 ? dim + input_dims.size() : dim;
    CHECK(dim >= 0 && dim < input_dims.size());
    auto output = std::make_shared<Tensor>(input_dims, dtype, input->GetDevice());

    switch (dtype) {
        DISPATCH_CASE(WRAP(LaunchForward<256, float>(output, input, dim);), DataType::kFLOAT32)
        DISPATCH_CASE(WRAP(LaunchForward<256, __maca_bfloat16>(output, input, dim);), DataType::kBFLOAT16)
    default:
        LOG_LOC(FATAL, "MACA softmax forward: 'Unsupported data type'");
    }
    return output;
}

template <size_t BLOCK_SIZE, typename T>
__global__ void SoftmaxBackwardKernel(T *grad_input, const T *grad_output, const T *output, int64_t outer_size,
                                      int64_t axis_size, int64_t inner_size) {
    using BlockReduce = cub::BlockReduce<float, BLOCK_SIZE>;

    __shared__ typename BlockReduce::TempStorage temp_storage_sum;
    __shared__ float row_sum;

    const int64_t group = blockIdx.x;
    const int64_t inner_idx = blockIdx.y;
    const int tid = threadIdx.x;

    // calculate the sum of the dot product of gradients
    float thread_sum = 0;
    for (int64_t axis = tid; axis < axis_size; axis += BLOCK_SIZE) {
        const int64_t idx = (group * axis_size + axis) * inner_size + inner_idx;
        thread_sum += common::maca::Cast<float>(grad_output[idx] * output[idx]);
    }
    float block_sum = BlockReduce(temp_storage_sum).Sum(thread_sum);

    if (tid == 0) {
        row_sum = block_sum;
    }
    __syncthreads();

    // update the input gradient
    for (int64_t axis = tid; axis < axis_size; axis += BLOCK_SIZE) {
        const int64_t idx = (group * axis_size + axis) * inner_size + inner_idx;
        grad_input[idx] = output[idx] * (grad_output[idx] - common::maca::Cast<T>(row_sum));
    }
}

template <size_t BLOCK_SIZE, typename T>
void LaunchBackward(const std::shared_ptr<Tensor> &grad_input, const std::shared_ptr<Tensor> &grad_output,
                    const std::shared_ptr<Tensor> &output, int64_t dim) {
    const auto &output_dims = output->Dims();
    int64_t outer_size = 1;
    int64_t axis_size = output_dims[dim];
    int64_t inner_size = 1;

    for (int i = 0; i < dim; ++i) { outer_size *= output_dims[i]; };
    for (int i = dim + 1; i < output_dims.size(); ++i) { inner_size *= output_dims[i]; };
    if (axis_size == 0) {
        LOG_LOC(INFO, "MACA softmax backward: 'output_dims[dim] == 0'");
        return;
    }
    if (outer_size == 0) {
        return;
    }

    T *grad_input_ptr = static_cast<T *>(grad_input->DataPtr());
    const T *grad_output_ptr = static_cast<const T *>(grad_output->DataPtr());
    const T *output_ptr = static_cast<const T *>(output->DataPtr());

    if (BLOCK_SIZE > 1024) {
        LOG_LOC(FATAL, "MACA softmax backward: 'BLOCK_SIZE used is larger than the max number of thread per block'");
    }
    dim3 block(BLOCK_SIZE);
    dim3 grid(outer_size, inner_size);

    const auto *maca_device = dynamic_cast<const MacaDevice *>(output->GetDevice());
    maca_device->SetDevice();
    SoftmaxBackwardKernel<BLOCK_SIZE, T><<<grid, block, 0, maca_device->Stream()>>>(
        grad_input_ptr, grad_output_ptr, output_ptr, outer_size, axis_size, inner_size);
}

std::shared_ptr<Tensor> SoftmaxBackward(const std::shared_ptr<Tensor> &grad_output,
                                        const std::shared_ptr<Tensor> &output, int64_t dim) {
    auto grad_output_dtype = grad_output->Dtype();
    auto output_dtype = output->Dtype();
    DataType promoted_type = DispatchFunc<DataTypeList<INFINI_ALL_TYPES>, DataTypeList<INFINI_ALL_TYPES>>(
        {grad_output_dtype, output_dtype},
        [=]<typename Tgrad, typename Tout>() { return DataTypeMap_v<WidestType_t<Tgrad, Tout>>; },
        "MACA SoftmaxBackward");

    auto grad_output_promoted
        = grad_output_dtype == promoted_type ? grad_output : std::make_shared<Tensor>(grad_output->To(promoted_type));
    auto output_promoted = output_dtype == promoted_type ? output : std::make_shared<Tensor>(output->To(promoted_type));

    const auto &output_dims = output->Dims();
    dim = dim < 0 ? dim + output->Dims().size() : dim;
    CHECK(dim >= 0 && dim < output->Dims().size());

    auto grad_input = std::make_shared<Tensor>(output_dims, promoted_type, output->GetDevice());
    DispatchFunc<INFINI_ALL_TYPES>(
        promoted_type, [=]<typename T>() { grad_input->Fill<T>(0); }, "MACA SoftmaxBackward");

    switch (promoted_type) {
        DISPATCH_CASE(WRAP(LaunchBackward<256, float>(grad_input, grad_output_promoted, output_promoted, dim);),
                      DataType::kFLOAT32)
        DISPATCH_CASE(WRAP(LaunchBackward<256, __maca_bfloat16>(grad_input, grad_output_promoted, output_promoted, dim);),
                      DataType::kBFLOAT16)
    default:
        LOG_LOC(FATAL, "MACA softmax backward: 'Unsupported data type'");
    }

    return grad_input;
}
} // namespace infini_train::kernels::maca

#define REGISTER_MACA_SOFTMAX_KERNEL(kernel_name)                                                                      \
    REGISTER_KERNEL(infini_train::DeviceType::kMACA, kernel_name, infini_train::kernels::maca::kernel_name)

REGISTER_MACA_SOFTMAX_KERNEL(SoftmaxForward)
REGISTER_MACA_SOFTMAX_KERNEL(SoftmaxBackward)

#undef REGISTER_MACA_SOFTMAX_KERNEL
